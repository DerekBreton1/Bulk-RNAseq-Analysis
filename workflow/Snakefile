files = ['Healthy_15', 'Healthy_72', 'Healthy_75', 'Severe_77', 'Severe_78', 'Severe_90', 'ICU_79', 'ICU_81', 'ICU_84']
readpairs = ['R1', 'R2']

rule all:
    input:
        'results/multiqc_report.html',
        expand('results/{name}/abundance.h5', name=files),
        expand('results/{name}/abundance.tsv', name=files)


# Initial quality control
rule fastqc:
    input:
        fastq = 'data/{name}_{read}.fastq.gz'
    output:
        fastqc = 'results/{name}_{read}_fastqc.html'
    params:
        outdir = 'results/'
    conda:
        'envs/fastqc_env.yaml'
    shell:
        '''
        fastqc {input.fastq} -o {params.outdir}
        '''

# Aggregate the fastqc files
rule multiqc:
    input:
        expand('results/{name}_{read}_fastqc.html', name = files, read = readpairs)
    output:
        report = 'results/multiqc_report.html'
    params:
        outdir = 'results/'
    conda:
        'envs/multiqc_env.yaml'
    shell:
        '''
        multiqc {params.outdir} -o {params.outdir}
        '''

# Download and merge cDNA and ncDNA
rule download_merge_DNA:
    output:
        transcriptome = 'results/Homo_sapiens.GRCh38.rna.fa'
    params:
        cDNA = 'ftp://ftp.ensembl.org/pub/current_fasta/homo_sapiens/cdna/Homo_sapiens.GRCh38.cdna.all.fa.gz',
        ncDNA = 'ftp://ftp.ensembl.org/pub/current_fasta/homo_sapiens/ncrna/Homo_sapiens.GRCh38.ncrna.fa.gz'
    shell:
        '''
        wget -P results/ {params.cDNA}
        wget -P results/ {params.ncDNA}
        zcat results/Homo_sapiens.GRCh38.cdna.all.fa.gz results/Homo_sapiens.GRCh38.ncrna.fa.gz > {output.transcriptome}
        '''

# Build a Kallisto index
rule build_kallisto_index:
    input:
        transcriptome = 'results/Homo_sapiens.GRCh38.rna.fa'
    output:
        index = 'results/hsGRCh38_kallisto.idx'
    conda:
        'envs/kallisto_env.yaml'
    shell:
        '''
        kallisto index -i {output.index} {input.transcriptome}
        '''

# Quantify abundances of transcripts
rule quant:
    input:
        index = 'results/hsGRCh38_kallisto.idx',
        r1 = 'data/{name}_R1.fastq.gz',
        r2 = 'data/{name}_R2.fastq.gz'
    output:
        abundance_h5 = 'results/{name}/abundance.h5',
        abundance_tsv = 'results/{name}/abundance.tsv'
    params:
        outdir = 'results/{name}'
    threads: 2
    conda:
        'envs/kallisto_env.yaml'
    shell:
        '''
        kallisto quant -i {input.index} -o {params.outdir} -t {threads} -b 50 {input.r1} {input.r2}
        '''