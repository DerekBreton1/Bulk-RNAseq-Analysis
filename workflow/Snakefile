sra_list = [line.strip() for line in open("data/SRR_Acc_List.txt")]
reads = [1, 2]

rule all:
    input:
        expand('data/{name}_{read}.fastq.gz', name=sra_list, read=reads),
        'results/multiqc_report.html',
        expand('results/{name}/abundance.h5', name=sra_list),
        "results/report.html"

# Download SRA files
rule sra_download:
    output:
        srr = 'data/{name}/{name}.sra'
    params:
        outdir = 'data/'
    conda:
        'envs/sra_toolkit_env.yaml'
    shell:
        '''
        python3 workflow/scripts/sra_download.py {wildcards.name} {params.outdir}
        '''
# FASTQ download from SRA ID file
rule fastq_download:
    input:
        srr = 'data/{name}/{name}.sra'
    output:
        r1 = 'data/{name}_1.fastq.gz',
        r2 = 'data/{name}_2.fastq.gz'
    params:
        outdir = 'data/'      
    conda:
        'envs/sra_toolkit_env.yaml'
    shell:
        "python3 workflow/scripts/fastq_download.py {input.srr} {params.outdir}"

# Initial quality control
rule fastqc:
    input:
        r1 = 'data/{name}_1.fastq.gz',
        r2 = 'data/{name}_2.fastq.gz'
    output:
        r1_qc = 'results/{name}_1_fastqc.html',
        r2_qc = 'results/{name}_2_fastqc.html',
    params:
        outdir = 'results/'
    conda:
        'envs/fastqc_env.yaml'
    shell:
        '''
        fastqc -o {params.outdir} {input.r1} {input.r2}
        '''

# Aggregate the fastqc files
rule multiqc:
    input:
        expand('results/{name}_{read}_fastqc.html', name = sra_list, read = reads)
    output:
        report = 'results/multiqc_report.html'
    params:
        outdir = 'results/'
    conda:
        'envs/multiqc_env.yaml'
    shell:
        '''
        multiqc {params.outdir} -o {params.outdir}
        '''

# Download and merge cDNA and ncDNA, also download GTF. All release 113
rule download_merge_DNA:
    output:
        transcriptome = 'results/Homo_sapiens.GRCh38.rna.fa'
    params:
        cDNA = 'ftp://ftp.ensembl.org/pub/release-113/fasta/homo_sapiens/cdna/Homo_sapiens.GRCh38.cdna.all.fa.gz',
        ncDNA = 'ftp://ftp.ensembl.org/pub/release-113/fasta/homo_sapiens/ncrna/Homo_sapiens.GRCh38.ncrna.fa.gz',
        gtf = 'ftp://ftp.ensembl.org/pub/release-113/gtf/homo_sapiens/Homo_sapiens.GRCh38.113.gtf.gz'
    shell:
        '''
        wget -P data/ {params.cDNA}
        wget -P data/ {params.ncDNA}
        wget -P data/ {params.gtf}
        zcat data/Homo_sapiens.GRCh38.cdna.all.fa.gz data/Homo_sapiens.GRCh38.ncrna.fa.gz > {output.transcriptome}
        '''

# Build a Kallisto index
rule build_kallisto_index:
    input:
        transcriptome = 'results/Homo_sapiens.GRCh38.rna.fa'
    output:
        index = 'results/hsGRCh38_kallisto.idx'
    conda:
        'envs/kallisto_env.yaml'
    shell:
        '''
        kallisto index -i {output.index} {input.transcriptome}
        '''

# Quantify abundances of transcripts
rule quant:
    input:
        index = 'results/hsGRCh38_kallisto.idx',
        r1 = 'data/{name}_1.fastq.gz',
        r2 = 'data/{name}_2.fastq.gz'
    output:
        abundance_h5 = 'results/{name}/abundance.h5'
    params:
        outdir = 'results/{name}'
    threads: 2
    conda:
        'envs/kallisto_env.yaml'
    shell:
        '''
        kallisto quant -i {input.index} -o {params.outdir} -t {threads} -b 50 {input.r1} {input.r2}
        '''

# Knit downstream analysis into an HTML file
# May need to change script to path/to/R/bin/Rscript.exe path/to/your/script.R on Windows 
rule knit_report:
    input:
        "data/SraRunTable.csv",
        "workflow/scripts/report.Rmd",
        expand('results/{name}/abundance.h5', name=sra_list)
    output:
        "results/report.html"
    shell:
        '''
        Rscript render_report.R
        '''